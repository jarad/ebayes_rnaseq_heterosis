
The following sections provide Stan model code for performing the optimization and MCMC described in Section \ref{s:ebayes}. In these models, it is easier to construct the model using a single index, i.e. $y_i$, and then provide vectors that provide relevant quantities for this observation. These vectors include $g$ which indicates the gene, $v$ which indicates the variety, and $s$ which gives the sample index, i.e. a distinct identifier for the variety-replicate combation. 

\subsection{Optimization models}

The following two sections provide Stan model code for the optimization procedure, using the {\tt optimizing} function in RStan, either assuming independence or estimating a covariance amongst the location and (log) dispersion parameters. 

\subsubsection{All genes model}
\label{s:all_genes_model}

This section provides Stan model code for the model that assumes prior independence amongst the location and dispersion parameters. 

\begin{verbatim}
data{
  int N; //total # obs
  int G; // # genes
  int S; // # samples
  int<lower=0> y[N];
  int<lower=1,upper=G> g[N]; //obs -> gene
  int<lower=1,upper=3> t[N]; //obs -> genotype
  int<lower=1,upper=S> s[N]; //obs -> sample (genotype*rep)
  row_vector[3] X[3]; // mean structure (genotype)
}
parameters{
  matrix[3,G] B;         //phi, alpha, gamma by gene
  vector[G] lpsi;        //'dispersion'
  vector[S] c;           //lane sequencing depth
  real<lower=-20,upper=20> mu_phi;
  real<lower=-20,upper=20> mu_alpha;
  real<lower=-20,upper=20> mu_delta;
  real<lower=-20,upper=20> mu_psi;
  real<lower=0,upper = 20> sigma_phi;
  real<lower=0,upper = 20> sigma_alpha;
  real<lower=0,upper = 20> sigma_delta;
  real<lower=0,upper = 20> sigma_psi;
  real<lower=0,upper=5> sigma_c;
}

model{
  lpsi ~ normal(mu_psi,sigma_psi);
  B[1] ~ normal(mu_phi,sigma_phi);
  B[2] ~ normal(mu_alpha,sigma_alpha);
  B[3] ~ normal(mu_delta,sigma_delta);
  c ~ normal(0,sigma_c);
  for(n in 1:N){
    y[n] ~ neg_binomial_2_log(X[t[n]]*col(B,g[n]) + c[s[n]], 1 / exp(lpsi[g[n]]));
  }
  sigma_psi ~ cauchy(0,3);
  sigma_phi ~ cauchy(0,3);
  sigma_alpha ~ cauchy(0,3);
  sigma_delta ~ cauchy(0,3);
}
\end{verbatim}

\subsubsection{All genes model with covariance}
\label{s:all_genes_model_with_covariance}

This section provides Stan model code for the model that estimates the covariance structure amongst the location and (log) dispersion parameters. 

\begin{verbatim}
data{
  int N; //total # obs
  int G; // # genes
  int S; // # samples
  int<lower=0> y[N];
  int<lower=1,upper=G> g[N]; //obs -> gene
  int<lower=1,upper=3> t[N]; //obs -> genotype
  int<lower=1,upper=S> s[N]; //obs -> sample (genotype*rep)
  row_vector[4] X[3]; // mean structure (genotype)
}
parameters{
  vector[4] B[G];         //mu1, mu2, mu3, lpsi by gene
  vector[S] c;            //lane sequencing depth
  vector<lower=-20,upper=20>[4] mu;
  cov_matrix[4] Sigma;
  real<lower=0,upper=5> sigma_c;
}

model{
  vector[4] x;
  for(i in 1:G){
    B[i] ~ multi_normal(mu, Sigma);
  }
  c ~ normal(0,sigma_c);
  for(n in 1:N){
    y[n] ~ neg_binomial_2_log(X[t[n]]*B[g[n]] + c[s[n]], 1 / exp(B[g[n],4]));
  }
  x <-rep_vector(1,4);
  Sigma ~ inv_wishart(5.0,diag_matrix(x));
}
\end{verbatim}

\subsection{Models for MCMC}

As described in Section \ref{s:ebayes}, conditional on the hyperparameters inference on the gene-specific parameters can be performed independently and in parallel. This section provides Stan model code for single-gene models either assuming independence or with a covariance structure amongst the location and (log) dispersion parameter. These model files are used in conjunction with the {\tt sampling} function in RStan. 

\subsubsection{Single-gene model assuming independence}
\label{s:single_gene_model}

This section provides Stan model code for the single-gene model where independence is assumed amongst the location and (log) dispersion parameters.

\begin{verbatim}
data{
  int N; //total # obs
  int S; // # samples
  int<lower=0> y[N];
  int<lower=1,upper=3> t[N]; //obs -> genotype
  int<lower=1,upper=S> s[N]; //obs -> sample (genotype*rep)
  row_vector[3] X[3]; // mean structure (genotype)
  real<lower=-20,upper=20> mu_phi;
  real<lower=-20,upper=20> mu_alpha;
  real<lower=-20,upper=20> mu_delta;
  real<lower=-20,upper=20> mu_psi;
  real<lower=0,upper = 20> sigma_phi;
  real<lower=0,upper = 20> sigma_alpha;
  real<lower=0,upper = 20> sigma_delta;
  real<lower=0,upper = 20> sigma_psi;
  vector[S] c;            //lane sequencing depth
}
transformed data{
  vector[3] Mu;
  cov_matrix[3] Sigma;
  Mu[1] <- mu_phi;
  Mu[2] <- mu_alpha;
  Mu[3] <- mu_delta;
  Sigma[1,2] <- 0;
  Sigma[2,1] <- 0;
  Sigma[1,3] <- 0;
  Sigma[3,1] <- 0;
  Sigma[2,3] <- 0;
  Sigma[3,2] <- 0;
  Sigma[1,1] <- square(sigma_phi);
  Sigma[2,2] <- square(sigma_alpha);
  Sigma[3,3] <- square(sigma_delta);
}
parameters{
  vector[3] B;         //phi, alpha, gamma by gene
  real lpsi;       //'dispersion'
}

model{
  lpsi ~ normal(mu_psi,sigma_psi);
  B    ~ multi_normal(Mu,Sigma);
  for(n in 1:N){
    y[n] ~ neg_binomial_2_log(X[t[n]]*B + c[s[n]], 1 / exp(lpsi));
  }
}
\end{verbatim}

\subsection{Single-gene model with covariance}
\label{s:single_gene_model_with_covariance}

This section provides Stan model code for the single-gene model where a covariance amongst the location and (log) dispersion parameters is used.

\begin{verbatim}
data{
  int N; //total # obs
  int S; // # samples
  int<lower=0> y[N];
  int<lower=1,upper=3> t[N]; //obs -> genotype
  int<lower=1,upper=S> s[N]; //obs -> sample (genotype*rep)
  row_vector[4] X[3]; // mean structure (genotype)
  vector[S] c;            //lane sequencing depth
  vector<lower=-20,upper=20>[4] mu;
  real<lower=0> sigma_phi;
  real<lower=0> sigma_alpha;
  real<lower=0> sigma_delta;
  real<lower=0> sigma_psi;
  real cor_12;
  real cor_13;
  real cor_14;
  real cor_23;
  real cor_24;
  real cor_34;
  real<lower=0,upper=5> sigma_c;
}
transformed data{
  cov_matrix[4] Sigma;
  Sigma[1,2] <- cor_12;
  Sigma[2,1] <- cor_12;
  Sigma[1,3] <- cor_13;
  Sigma[3,1] <- cor_13;
  Sigma[1,4] <- cor_14;
  Sigma[4,1] <- cor_14;
  Sigma[2,3] <- cor_23;
  Sigma[3,2] <- cor_23;
  Sigma[2,4] <- cor_24;
  Sigma[4,2] <- cor_24;
  Sigma[3,4] <- cor_34;
  Sigma[4,3] <- cor_34;
  Sigma[1,1] <- sigma_phi;
  Sigma[2,2] <- sigma_alpha;
  Sigma[3,3] <- sigma_alpha;
  Sigma[4,4] <- sigma_psi;
}
parameters{
  vector[4] B;         //phi, alpha, gamma by gene
}

model{
  B    ~ multi_normal(mu,Sigma);
  for(n in 1:N){
    y[n] ~ neg_binomial_2_log(X[t[n]]*B + c[s[n]], 1 / exp(B[4]));
  }
}
\end{verbatim}
